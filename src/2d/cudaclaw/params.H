#ifndef __PARAMS_H__
#define __PARAMS_H__

#ifdef CUDA

#include "real.H"
#include "common.H"
#include "clawpack_mempool.H"
#include "clawpack_device.H"
#include "clawpack_CUDA_helper.H"

#include <assert.h>
#include <cuda_runtime.h>
#include <stdio.h>
#include <string>
#include <cstring>



class pdeParam
{
    public:
        int cellsX;			// number of cells on the horizontal, including ghost cells (unpadded)
        int cellsY;			// number of cells on the vertical, including ghost cells
        const int numGhostCells;	// ghost cells for the boundaries
        const int numStates;	// number of states of the problem, number of scalars per cell
        const int numWaves;		// number of nonzero waves generated by the Riemann solver (usually as many as there are equations)
        const int numCoeff;		// number of different types medium properties

        bool second_order;		// second order accuracy computation flag

        real width;		       	// physical domain's width
        real height;       		// physical domain's height;
        real dx;		       	// physical domain's spacial discritization size on the horizontal
        real dy;		       	// physical domain's spacial discritization size on the vertical
        const real startX;		// physical domain's starting x in cartesian coordinates
        const real endX;		// physical domain's ending x in cartesian coordinates
        const real startY;		// physical domain's starting y in cartesian coordinates
        const real endY;		// physical domain's ending y in cartesian coordinates

        real dt;			// time step to be used in the computation

        real* coefficients;		// physical medium coefficients

        // GPU residents
        real* q;			// data, cells' states
        real* qNew;		       	// intermediate update holder

        // real* wavesX;		// horizontal waves
        // real* wavesY;		// vertical waves

        // Each entry in waveSpeedsX stores maximum u-velocity in one CUDA block. So the size
        // of waveSpeedsX should be number of CUDA blocks for this AMR grid patch
        // The same rule applies to waveSpeedsY
        // after calling reduceMax_simplified, waveSpeedsX[0] has the maximum u velocity on this grid, and
        // waveSpeedsY[0] has the maximum v velocity on this grid.
        real* waveSpeedsX;		// speed of horizontal waves
        real* waveSpeedsY;		// speed of vertical waves

        real* cfl_grid; // an array of size 1xSPACEDIM, storing cfl number in current grid patch in all directions

        const int dev_id; // ID of the device on which storage are allocated
        const int id; // local id of this grid

        class TagAndDev 
        {
            public:
                int tag;
                int dev_id;
        };

        pdeParam(int cellsX, int cellsY, int numGhostCells, int numStates, int numWaves, int numCoeff,
                real startX, real endX, real startY, real endY,
                real dt_,
                real* q_, real* qNew_, 
                real* waveSpeedsX_, real* waveSpeedsY_,
                real* coefficients_,
                real* cfl_grid_, const int id_, int dev_id_):
                cellsX(cellsX), cellsY(cellsY), numGhostCells(numGhostCells), numStates(numStates), numWaves(numWaves), numCoeff(numCoeff),
                startX(startX), endX(endX), startY(startY), endY(endY), dt(dt_),
                q(q_), qNew(qNew_), waveSpeedsX(waveSpeedsX_), waveSpeedsY(waveSpeedsY_),
                coefficients(coefficients_), cfl_grid(cfl_grid_), id(id_), dev_id(dev_id_)
        {

            assert( (cellsX-2*numGhostCells) > 0 );
            assert( (cellsY-2*numGhostCells) > 0 );

            width = endX-startX;
            height = endY-startY;
            dx = width/cellsX;
            dy = height/cellsY;

            second_order = true;

        }

        // copy constructor
        pdeParam (const pdeParam&) = default;

        // assignment constructor
        pdeParam& operator=(const pdeParam& other) = delete;

        ~pdeParam() {}

        void static CUDART_CB cudaCallback_release_gpu(cudaStream_t event, cudaError_t status, void *data) {
            checkCudaErrors(status);
            TagAndDev* td = static_cast<TagAndDev*>(data);
            clawpack_mempool_release_gpu(td->tag, td->dev_id);
            delete td;
        }


        void setOrderOfAccuracy(int order)
        {
            if(order == 2)
                second_order = true;
            else
                second_order = false;
        }


        // GETTER AND SETTER FUNCTIONS

        // Q
        // Dictates how the memory layout for q will be
        __device__ __host__ 
        inline int getIndex_q(int row, int column, int state)
        {
            // column => cellsX, row => cellsY
            //
            // SOA
            // state is the slowest moving dimension now, then row, then column
            // return (state*cellsX*cellsY + row*cellsX + column);
            //
            // AOS
            return (state + column*NUMSTATES + row*NUMSTATES*cellsX);
        }
        inline __device__ real &getElement_q(int row, int column, int state)
        {
            return q[getIndex_q(row, column, state)];
        }
        inline __device__ void setElement_q(int row, int column, int state, real setValue)
        {
            q[getIndex_q(row, column, state)] = setValue;
        }

        // QNEW
        // Dictates how the memory layout for qNew will be
        inline __device__ int getIndex_qNew(int row, int column, int state)
        {
            // column => cellsX, row => cellsY
            //
            // SOA
            // state is the slowest moving dimension now, then row, then column
            // return (state*cellsX*cellsY + row*cellsX + column);
            //
            // AOS
            return (state + column*NUMSTATES + row*NUMSTATES*cellsX);
        }
        inline __device__ real &getElement_qNew(int row, int column, int state)
        {
            return qNew[getIndex_qNew(row, column, state)];
        }
        inline __device__ void setElement_qNew(int row, int column, int state, real setValue)
        {
            qNew[getIndex_qNew(row, column, state)] = setValue;
        }


        // COEFFICIENTS
        inline __device__ __host__ int getIndex_coeff(int row, int column, int coeff)
        {
            // Usual C/C++ row major order
            // coeff is the slowest moving dimension now, then row, then column
            return (coeff*cellsX*cellsY + row*cellsX + column);
        }

        inline __device__ real &getElement_coeff(int row, int column, int coeff)
        {
            return coefficients[getIndex_coeff(row, column, coeff)];
        }

        inline __device__ void setElement_coeff(int row, int column, int coeff, real setValue)
        {
            coefficients[getIndex_coeff(row, column, coeff)] = setValue;
        }

        inline void alloc_waveSpeedsX(size_t nWords) {
            size_t sz = nWords*sizeof(real);
            waveSpeedsX = (real*) clawpack_mempool_alloc_gpu_hold(sz, this->dev_id, this->id);
        }

        inline void alloc_waveSpeedsY(size_t nWords) {
            size_t sz = nWords*sizeof(real);
            waveSpeedsY = (real*) clawpack_mempool_alloc_gpu_hold(sz, this->dev_id, this->id);
        }

        // add cuda callback function to free memory allocated and managed by this pdeParam object
        // The memory will be free after all CUDA events in the stream before this function call are finished
        inline void register_allocated_memory() {
            cudaStream_t stream;
            get_cuda_stream(this->id, this->dev_id, &stream);

            TagAndDev* td = new TagAndDev();
            td->tag = this->id;
            td->dev_id = this->dev_id;
            cudaStreamAddCallback(stream, pdeParam::cudaCallback_release_gpu, (void*) td, 0);
        }

};

#endif

#endif
